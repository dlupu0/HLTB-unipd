1. How many games per platform ?


PREFIX : <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
#how many games per platform


select distinct ?platform (COUNT(?game) as ?games) where{
           ?game :releasedOn ?platform.
    }
GROUP BY ?platform
HAVING (?games > 10)
ORDER BY DESC (?games)


2. Do people play the same game the most time on Nintendo Switch or on PC?


PREFIX : <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
#check if people play the same game the most time on nintendo switch or pc (in seconds)?


select distinct (COUNT(?game) as ?totalGames) (xsd:integer(AVG(?switch)) as ?averageTimeNintendoSwitch) (xsd:integer(AVG(?avgPC)) as ?averageTimePC) where {
        select distinct ?game (AVG(?switchTime) as ?switch) ?avgPC where{
                ?game :hasStats ?stats.
                ?stats :mainTime ?switchTime;
                       :onPlatform :nintendo-switch.
                FILTER(?switchTime > 0)
                {
                        select distinct ?game (AVG(?pcTime) as ?avgPC) where{
                                ?game :hasStats ?stats.
                                ?stats :mainTime ?pcTime;
                                                    :onPlatform :pc.
                                ?game :releasedOn :pc;
                                      :releasedOn :nintendo-switch;
                                      :releasedOn ?platform.
                                FILTER(?pcTime > 0)
                }
                GROUP BY ?game
                }}
        GROUP BY ?avgPC ?game
}






3. Are the number of games released before 2013 on pc more than the ones released after 2013?


PREFIX : <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
#Are the number of games released before 2013 on pc more than the ones released after 2013?


PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
ask where{
        {
    select distinct (COUNT(?game) as ?after2013) where {
            ?game :multiplayerFocus true;
                    :releasedOn ?platform;
                   :hasStats ?stats.
        ?stats :releaseDate ?date.
        ?platform :name "PC".
        FILTER("2013"^^xsd:dateTime<?date)
        }}
        {
    select distinct (COUNT(?game) as ?before2013) where {
            ?game :multiplayerFocus true;
                            :releasedOn ?platform;
                            :hasStats ?stats.
        ?stats :releaseDate ?date.
        ?platform :name "PC".
        FILTER("2013"^^xsd:dateTime>=?date)
            }}    
FILTER(?before2013>?after2013)
}


4. How many adventure games are released on pc?


PREFIX : <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
#released adventure games per year on pc


select distinct ?year (COUNT(?game) as ?games)  where{
        ?game  :hasStats ?stats;
                       :hasGenre ?genre.
        ?stats :releaseDate ?date;
                       :onPlatform :pc.
        ?genre :name ?name.
 FILTER (REGEX(?name,".*adv.*"))
}
GROUP BY (year(?date) as ?year)
ORDER BY DESC(?year)


5.  How many copies sold the games for which the users inserted the most completion time data? (likely the most appreciated)


PREFIX : <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
#units sold worldwide on all platforms for the most polled games (likely appreciated games by fans)
select distinct ?game (SUM(?people) as ?howManyPolled) (SUM(?units) as ?unitsSold) where{
        ?game :hasStats ?stats.
        ?stats :polledTime ?people.
    
         ?game :sold ?sales.
         ?sales :unitsSold ?units;
                       :locatedIn ?region;
                       :onPlatform ?platform.
        FILTER(?units > 0)
}
GROUP BY ?game
ORDER BY DESC (?howManyPolled)
LIMIT 10


6. How many exclusive games each popular platform has?


PREFIX : <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
#exclusive games on popular platforms
select distinct ?name (COUNT(?game) as ?games)  where {
        ?game :releasedOn ?platform.
        ?platform :officialName ?name;
                  :popular true.
          {
        select distinct ?game (COUNT(?platform) as ?platforms) where{
                ?game :releasedOn ?platform;
                      :officialName ?name.
        }
        GROUP BY ?game ?name
        HAVING (?platforms = 1)
        }
}
GROUP BY ?name


7. What are the most praised games by the critic?


PREFIX : <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
#games with a very high critic score grouped by year


select distinct ?year (GROUP_CONCAT(DISTINCT ?name; separator = ", ") as ?games) where{
    ?game :hasStats ?stats;
          :officialName ?name.
    ?stats :releaseDate ?date;
           :criticScore ?score.
 FILTER(?score >= 9.5) 
}
GROUP BY (year(?date) as ?year)
ORDER BY DESC (?year)


8. What multiplayer focused games of Electronic Arts sold the most?


PREFIX : <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
#all multiplayer focused games of Electronic Arts
select distinct ?game (SUM(?soldUnits) as ?totalUnits) where{
    ?game a :Game;
          :officialName ?name;
              :publishedBy ?company;
                 :multiplayerFocus true;
                     :sold ?sales.
OPTIONAL {?sales :unitsSold ?soldUnits.} #if for a given platform we don't have sales data
FILTER (REGEX(str(?company),"ea-") || REGEX(str(?company),"electronic-arts"))
}
GROUP BY ?game ?company
HAVING (?totalUnits > 0.4)
ORDER BY DESC (?totalUnits)




9. How many companies per region?


PREFIX : <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
#how many companies per region


select distinct ?region (COUNT(?company) as ?howManyCompanies)  where{
            ?company :basedIn ?country.
            ?country :locatedIn ?region.
}
GROUP BY ?region 
ORDER BY ?howManyCompanies


10. How long does it take to beat the most played Pokèmon games?


PREFIX : <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
#how long to beat every most played pokémon game


select distinct ?name (xsd:integer(AVG(?mainStory/3600)) as ?avgMainStory_hours) (xsd:integer(AVG(?completionistTime/3600)) as ?avgCompletionistTime_hours)  where{
    ?game :hasStats ?stats;
          :officialName ?name.
    ?stats :mainTime ?mainStory;
           :completionistTime ?completionistTime;
           :polledTime ?polledTime.
    FILTER(REGEX(?name, "(?i)pokémon*"))
    FILTER(?polledTime > 100) #at least 100 people inserted data - likely the most played titles
}
GROUP BY ?name
ORDER BY DESC (?avgMainStory_hours)


11. How many bits is the cpu that runs the given game? (CONSTRUCT Query)


PREFIX hltb: <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
#How many bits is the cpu that runs the given game?
#example of construct query.


CONSTRUCT {
    ?game rdf:type hltb:Game . 
    ?game hltb:officialName ?name .
    ?platform hltb:bits ?bit .


}WHERE{
    ?game hltb:officialName ?name ;
       hltb:releasedOn ?platform .
    ?platform hltb:bits ?bit .
}




12. (Bonus Query) Data about Fifa and PES.


PREFIX : <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
#time to beat the career, the critic score and sold units for "fifa" and "pro evolution soccer"


select distinct ?name  (xsd:integer(AVG(?carriera/3600)) as ?carrerTime_hours) (AVG(?score) as ?criticScore) (SUM(DISTINCT ?sales) as ?soldUnits) where{
    ?game :officialName ?name.
    OPTIONAL{
        ?game :hasStats/:mainTime ?carriera.
    }
    OPTIONAL{?game :sold/:unitsSold ?sales.}
    OPTIONAL{?game :hasStats/:criticScore ?score.}
    FILTER(REGEX(?name,"(?i)fifa \\d") || REGEX(?name, "(?i)pro evolution soccer") || REGEX(?name,"\\d (?i)fifa"))
}
GROUP BY ?name 



13.Show the game named "007 Legends"

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX hltb: <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
#Show the game named "007 Legends"

SELECT ?game WHERE 
{ 
	?game hltb:officialName "007 Legends"^^xsd:string .
} 


14. List the games whose platform has 32-bits or 8-bits CPU

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX hltb: <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
#List the games whose platform has 32-bits or 8-bits CPU

SELECT ?name ?platform ?bit WHERE { 
	?game hltb:officialName ?name ;
       hltb:releasedOn ?platform .
    ?platform hltb:bits ?bit .
    FILTER(?bit = "32"^^xsd:int || ?bit = "8"^^xsd:int) . 
	
} limit 100 


15. Are the average number of games sold in Europe more than North America?

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX hltb: <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
#Are the average number of games sold in Europe more than North America?

ASK WHERE{
    {
        SELECT ( AVG(?unitsSold) AS ?totsale_eu) WHERE{
        	?game  hltb:officialName ?name ;
                hltb:locatedIn hltb:eu ;
       			hltb:sold ?placeplatform .
    		?placeplatform hltb:unitsSold ?unitsSold .
    	}
    }
    {
        SELECT ( AVG(?unitsSold) AS ?totsale_na) WHERE{
        	?game  hltb:officialName ?name ;
                hltb:locatedIn hltb:na ;
       			hltb:sold ?placeplatform .
    		?placeplatform hltb:unitsSold ?unitsSold .
    	}
    } 
FILTER (?totsale_eu > ?totsale_na)
}

16. list of games and their platforms sold in Japan and North America

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX hltb: <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
#list of games and their platforms sold in Japan and North America

SELECT ?name ?placeplatform ?unitsSold WHERE{
	{
    ?game hltb:officialName ?name ;
       hltb:sold ?placeplatform .
    ?placeplatform hltb:unitsSold ?unitsSold ;
    	hltb:locatedIn 	hltb:jp .
	}
UNION
	{
    ?game hltb:officialName ?name ;
       hltb:sold ?placeplatform .
    ?placeplatform hltb:unitsSold ?unitsSold ;
    	hltb:locatedIn 	hltb:na .
	}
}
ORDER BY ?unitsSold





ANALYTICS QUERIES


1. Number of games in the databases, for how many we have Stats data, for how many we have Sales data and for how many both. 


PREFIX : <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
#number of games in the databases, for how many we have Stats data, for how many we have Sales data and for how many both. 


select distinct (COUNT(DISTINCT?game2) as ?totalGames)(COUNT(DISTINCT?game1)  as ?gamesWithStatsData) (COUNT(DISTINCT?game) as ?gamesWithSalesData)  (COUNT(DISTINCT?game3) as ?gamesWithBothData)where {
    {
    ?game :sold ?sales.  
        }
        UNION
    {
                ?game1 :hasStats ?stats.
    }
    UNION
    {
        ?game2 :id ?id.
    }
    UNION
    {
        ?game3 :sold ?sales;
               :hasStats ?stats.    
    }
}




2. Triples, edges, nodes and attributes in the graph. 


PREFIX : <http://www.dei.unipd.it/database2/HLTB-db2unipd#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
#how many triples,edges,nodes and attributes in the graph


SELECT DISTINCT (COUNT (?l) as ?totalTriples)  (COUNT (?v) as ?totalNodes)  (COUNT (?s) as ?totalAttributes) (COUNT (?b) as ?totalEdges)WHERE
{
    {
          ?l ?n ?f
    }
    UNION
    {
          ?c a owl:DatatypeProperty.
            ?s ?c ?o.
    }
    UNION
        {
    ?d a owl:ObjectProperty.
    ?b ?d ?e. 
        }
    UNION
        {
    ?z a owl:Class.
    ?v ?x ?z. 
        }
}